# Maintainer: Zifei Tong <soariez@gmail.com>

pkgname=dyninst
pkgver=8.1.2
pkgrel=1
pkgdesc="An API for run-time code generation"
url="http://www.dyninst.org"
arch=('x86_64' 'i686')
license=('LGPL')
depends=('boost' 'libdwarf')
install="dyninst.install"
source=("http://www.dyninst.org/sites/default/files/downloads/${pkgname}/${pkgver}/DyninstAPI-${pkgver}.tgz"
        "dyninst_linux-vdso.patch")
sha1sums=('0bda1b08bea34ddda6fbcfd94622beb54b816acf'
          '58475c200cec5e7aad75e4c44e9309d020153f3e')

build() {
  cd "$srcdir/DyninstAPI-$pkgver"
  patch -Np1 -i $srcdir/dyninst_linux-vdso.patch
  ./configure --includedir=/usr/include/dyninst --libdir=/usr/lib/dyninst
  make
}

package() {
  cd "$srcdir/DyninstAPI-$pkgver"
  make DESTDIR="$pkgdir" install

  install -dm755 $pkgdir/etc/ld.so.conf.d/
  echo '/usr/lib/dyninst' > ${pkgdir}/etc/ld.so.conf.d/dyninst.conf

  chmod 644 $pkgdir/usr/include/dyninst/*
  chmod 644 $pkgdir/usr/lib/dyninst/*.a
}
